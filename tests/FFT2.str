
void->void pipeline FFT2() {
  //add FFTTestSource(64);
  add FileReader<int>();
  add FFTKernel2(64);
  //add FloatPrinter();
  add FileWriter<int>("output.stream");
}


int->int filter CombineDFT(int n) {

  // coefficients, real and imaginary interleaved
  int[n] w;

  init {
      //int wn_r = (int)cos(2 * 3 / n);
      //int wn_i = (int)sin(-2 * 3 / n);
	  
	  int wn_r = -1;
      int wn_i = 0;
	  
	  if(n==2){
	    wn_r = -8192;
        wn_i = 0;
	  }
	  else if(n==4){
	    wn_r = 0;
        wn_i = -8192;
	  }
	  else if(n==8){
	    wn_r = 5793;
        wn_i = -5793;
	  }
	  else if(n==16){
	    wn_r = 7569;
        wn_i = -3135;
	  }
	  else if(n==32){
	    wn_r = 8035;
        wn_i = -1598;
	  }
	  else if(n==64){
	    wn_r = 8153;
        wn_i = -803;
	  }
	  
	  int real = 10000;
      int imag = 0;
      int next_real, next_imag;
	  
      for (int i=0; i<n; i+=2) {
	    w[i] = real;
	    w[i+1] = imag;
	    next_real = real * wn_r - imag * wn_i;
	    next_imag = real * wn_i + imag * wn_r;
	  
	    real = next_real;
	    imag = next_imag;
	  
	    if(i>-1){
	      real = real >> 13;
	      imag = imag >> 13;
	    }
      }
  }

  work push 2*n pop 2*n {
        int i;
	int[2*n] results;

        for (i = 0; i < n; i += 2)
        {
	    // this is a temporary work-around since there seems to be
	    // a bug in field prop that does not propagate nWay into the
	    // array references.  --BFT 9/10/02
	    
	    //int tempN = nWay;
	    //Fixed --jasperln
            
            // removed nWay, just using n  --sitij 9/26/03

	    int i_plus_1 = i+1;

	    int y0_r = peek(i);
            int y0_i = peek(i_plus_1);
            
	    int y1_r = peek(n + i);
            int y1_i = peek(n + i_plus_1);

	    // load into temps to make sure it doesn't got loaded
	    // separately for each load
	    int weight_real = w[i];
	    int weight_imag = w[i_plus_1];

            int y1w_r = y1_r * weight_real - y1_i * weight_imag;
            int y1w_i = y1_r * weight_imag + y1_i * weight_real;

            results[i] = y0_r + y1w_r;
            results[i + 1] = y0_i + y1w_i;

	    results[n + i] = y0_r - y1w_r;
            results[n + i + 1] = y0_i - y1w_i;
        }

        for (i = 0; i < 2 * n; i++)
        {
            pop();
            push(results[i]);
        }
    }

}


int->int filter FFTReorderSimple(int n) {

  int totalData;

  init {
    totalData = 2*n; 
  }

  work push 2*n pop 2*n {
        int i;
        
        for (i = 0; i < totalData; i+=4)
        {
            push(peek(i));
            push(peek(i+1));
        }
        
        for (i = 2; i < totalData; i+=4)
        {
            push(peek(i));
            push(peek(i+1));
        }
        
        for (i=0;i<n;i++)
        {
            pop();
            pop();
        }
    }
}


int->int pipeline FFTReorder(int n) {

  for(int i=1; i<(n/2); i*= 2)
    add FFTReorderSimple(n/i);

}


int->int pipeline FFTKernel1(int n) {

  if(n>2) {
    add splitjoin {
      split roundrobin(2);
      add FFTKernel1(n/2);
      add FFTKernel1(n/2);
      join roundrobin(n);
    }
  }
  add CombineDFT(n);
}


int->int splitjoin FFTKernel2(int n) {

  split roundrobin(2*n);
  for(int i=0; i<2; i++) {
    add pipeline {
      add FFTReorder(n);
      for(int j=2; j<=n; j*=2)
        add CombineDFT(j);
    }
  }
  join roundrobin(2*n);
}


void->int filter FFTTestSource(int N) {

  work push 2*N pop 0 {
    int i;
    push(0);
    push(0);
    push(1);
    push(0);

    for(i=0; i<2*(N-2); i++)
      push(0);
  }
}


int->void filter FloatPrinter
{
    work push 0 pop 1 {
      println(pop());
    }
}